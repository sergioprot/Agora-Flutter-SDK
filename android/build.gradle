plugins {
    id "com.android.library"
    id "kotlin-android"
}

group 'io.agora.agora_rtc_engine'
version '1.0-SNAPSHOT'

def safeExtGet(prop, fallback) {
  rootProject.ext.has(prop) ? rootProject.ext.get(prop) : fallback
}

android {
    namespace "io.agora.agora_rtc_engine"
    compileSdkVersion safeExtGet('compileSdkVersion', 35)

    defaultConfig {
        minSdkVersion safeExtGet('minSdkVersion', 16)
        targetSdkVersion safeExtGet('targetSdkVersion', 35)

        consumerProguardFiles 'consumer-rules.pro'
    }

    compileOptions {
        sourceCompatibility JavaVersion.VERSION_17
        targetCompatibility JavaVersion.VERSION_17
    }

    kotlinOptions {
        jvmTarget = '17'
    }

    sourceSets {
        main.java.srcDirs += 'src/main/kotlin'

        if (isDev(project)) {
            main.jniLibs.srcDirs += 'libs'
        }
    }
}

dependencies {
    if (isDev(project)) {
        api fileTree(dir: "libs", include: ["*.jar", "*.aar"])
    } else {
        api 'io.agora.rtc:full-sdk:3.7.0.3'
        api 'io.agora.rtc:iris-rtc:3.7.0.3-rc.1'
    }

    implementation 'io.agora.rtc:full-screen-sharing:3.7.0.3'
}

static boolean isDev(Project project) {
    def devFile = project.file('.plugin_dev')
    return devFile.exists()
}
